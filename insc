#!/usr/bin/perl

use strict;
use warnings;

use POSIX qw(strftime);
use Term::ReadKey qw(ReadMode ReadKey);
use IPC::Run3;
use Getopt::Long
    qw(:config posix_default gnu_compat require_order bundling no_ignore_case);

sub usage;
sub fatal;
sub quit;
sub tty;
sub run;

use constant IMTMPL => 'i%04d.pnm';
use constant MDTMPL => 'm%04d.%s';
use constant IMGLOB => 'i[0-9][0-9][0-9][0-9].*';
use constant MDGLOB => 'm[0-9][0-9][0-9][0-9].*';

my $rxpath    = qr{^(\.\.?|\+)$|/};
my $rxnum     = qr{([0-9]+(?:\.[0-9]+)?)};
my $rxrange   = qr{^([0-9]+)(?:-([0-9]*)?)$};
my $rximfile  = qr{^i([0-9]{4})\.([^.]+)(?:\.xz)?$};

my $prog = 'scanadf';
my $tty;
my @now;
my ($rcfile, $device, $dryrun, $verbose);
GetOptions(
    'c|config-file=s' => \$rcfile,
    'd|device=s' => \$device,
    'n|dry-run' => \$dryrun,
    'v|verbose' => \$verbose,
) or usage;
$device = $ENV{'SANE_DEFAULT_DEVICE'} if !defined $device && $ENV{'SANE_DEFAULT_DEVICE'};

my %cmd = map { /^cmd_(.+)/ ? ($1 => main->can($_)) : () } keys %main::;

my ($mode, $source, $dpi, $paper, $width, $height, @path, %prop, %opt);
my @params = qw( gray 200dpi 215.889x279.400 adf );

($rcfile) = grep -e, map { glob $_ } qw(~/etc/insc/insc.conf ~/.inscrc /usr/local/insc/insc.conf /etc/insc/insc.conf /dev/null)
    if !defined $rcfile;
my %config = %{ kvread($rcfile) };
push @params, split /\s+/, $config{'params'} if defined $config{'params'};

my $root  = $config{'root' } // $ENV{'INSC_ROOT' } // glob('~/etc/insc') // '/usr/local/insc';
my $spool = $config{'spool'} // $ENV{'INSC_SPOOL'} // glob('~/var/insc') // '/var/local/insc';

# --- Global variables

#chdir $root or fatal;

my $cmd = \&cmd_scan;
if (@ARGV) {
    $cmd = $cmd{shift @ARGV} if $cmd{$ARGV[0]};
    $ARGV[0] =~ s{^%}{$root/projects/} if @ARGV;
}
$cmd->();

# --- Commands

sub cmd_init {
    parse(@params, @ARGV);
    my $path = path(@path);
    init($path);
    print $path, "\n";
}

sub cmd_add {
    usage if !@ARGV;
    my $path = shift @ARGV;
    chdir $path or fatal "Can't chdir $path: $!";
    my $proj = kvread('m0000.project');
    parse(split /\s+/, $proj->{'params'}) if defined $proj->{'params'};
    parse(@ARGV);
    fatal "Stray bits at end of args: @path" if @path;
    scan($path);
}

sub cmd_scan {
    goto &cmd_add if @ARGV
                  && $ARGV[0] =~ $rxpath
                  && -e "$ARGV[0]/m0000.project";
    @ARGV = ( '@default', '+' ) if !@ARGV;
    parse(@params, @ARGV);
    my $path = path(@path);
    if (-e "$path/m0000.project") {
        chdir $path or fatal "Can't chdir $path: $!";
    }
    else {
        init($path);
    }
    scan($path);
}

sub cmd_merge {
    #@ merge %PROJECT|/PATH FIRST LAST
    my $path = path(shift @ARGV);
    chdir $path or fatal "Can't chdir $path: $!";
    my $first = $ARGV[0];
    my $last  = $ARGV[-1];
    usage if $first >= $last;
    my @jobs = grep -e, map { sprintf mdfile($_, 'job') } $first..$last;
    my @ends = grep -e, map { sprintf mdfile($_, 'end') } $first..$last;
    fatal "Not the beginning of a job: $first"
        if $jobs[0] ne mdfile($first, 'job');
    fatal "Not the end of a job: $last"
        if $ends[-1] ne mdfile($last, 'end');
    fatal "Can't merge a single job" if !@jobs;
    my (@titles, $first_job);
    foreach my $j (@jobs) {
        my $job = kvread($j);
        $first_job ||= $job;
        push @titles, $job->{'prop.title'} if defined $job->{'prop.title'};
    }
    @titles = unique(@titles);
    my $n = 0;
    tty "Choose a title, type a new one, or enter Q to cancel:\n\n";
    tty sprintf "%4d : %s\n", $n++, $_ for @titles;
    tty "\nYour choice: ";
    my $t = <$tty>;
    chomp $t;
    quit 'Cancelled' if $t =~ /^[Qq]$/;
    $t =~ s{^[0-9]+$}{$titles[$t] // quit 'Cancelled'}e;
    $first_job->{'prop.title'} = $t;
    my $first_end = kvread($ends[0]);
    kvwrite(shift @jobs, $first_job);
    my $last_end = kvread($ends[-1]);
    $last_end->{'first'} = $first;
    $last_end->{'count'} = $last - $first + 1;
    kvwrite(pop @ends, $last_end);
    unlink @jobs, @ends or fatal "Merge failed: $!";
}

sub cmd_jobs {
    usage if @ARGV != 1;
    usage if $ARGV[0] !~ $rxpath;
    my $path = path(shift @ARGV);
    chdir $path or fatal "Can't chdir: $!";
    foreach my $job (jobs()) {
        my $title = $job->{'prop.title'} // '(untitled)';
        printf "%4d %4d %s\n", $job->{'first'}, $job->{'last'}, $title;
    }
}

sub cmd_lj { goto &cmd_jobs }

sub cmd_view {
    usage if @ARGV < 1;
    usage if $ARGV[0] !~ $rxpath;
    my $path = path(shift @ARGV);
    chdir $path or fatal "Can't chdir: $!";
    @ARGV = map { num($_) } glob(IMGLOB) if !@ARGV;
    my @images = map { sprintf IMTMPL, $_ } @ARGV;
    foreach (@images) {
        next if -e;
        my $xz = $_ . '.xz';
        fatal "Can't find image $_" if ! -e $xz;
        print STDERR "Decompressing $_\n";
        fatal "Can't unxz $xz: $!" if system('unxz', $xz) != 0;
    }
    system 'sxiv', @images;
}

sub cmd_make {
    usage if !@ARGV || $ARGV[0] !~ m{^(\.\.?)$|/|\.pdf$};
    my (%dest2images, $dest, @images);
    my $path = path(shift @ARGV);
    if ($path =~ s/\.pdf$//) {
        # insc make $projdir.pdf
        usage if @ARGV;
        fatal "Not a project: $path" if !-e "$path/m0000.project";
        @images = glob(IMGLOB);
        $dest2images{"$path.pdf"} = \@images;
    }
    chdir $path or fatal "Can't chdir $path: $!";
    foreach my $arg (@ARGV) {
        if ($arg =~ /^(\d+)(?:-(\d+))(\.pdf)?$/) {
            # insc make $projdir 1-7
            # insc make $projdir 1-7.pdf
            my @i = map { sprintf(IMTMPL, $_) } $1 .. ($2||$1);
            $arg .= '.pdf' if !defined $3;
            if ($arg !~ m{/}) {
                $arg = "made/$arg.pdf";
                -d 'made' or mkdir 'made' or fatal "Can't mkdir made: $!";
            }
            $dest2images{$arg} = \@i;
        }
        elsif ($arg =~ /^\.pdf$/) {
            # insc make $projdir .pdf
            usage if %dest2images || @ARGV;
            $dest2images{"$path.pdf"} = \@images;
        }
        elsif ($arg =~ /\.pdf$/) {
            # insc make $anydir/$anyfile.pdf
            $dest2images{$arg} = \@images;
        }
        else {
            usage;
        }
    }
    @images = glob(IMGLOB) if !@images;
    $dest2images{$dest} = \@images if !%dest2images;
    my $err = 0;
    while (my ($d, $i) = each %dest2images) {
        fatal "Won't overwrite $_" if -e $d;
        my @cmd = ('gm', 'convert', @$i, $d);
        dryrun(@cmd), next if $dryrun;
        run3 @cmd, \undef, \undef, \undef;
        $err ||= $?;
    }
    fatal "make failed: $!" if $err;
}

sub cmd_show {
    parse(@params, @ARGV);
    my $path = path(@path);
    my $params = params2str();
    if (-d $path) {
        chdir $path or fatal "Can't chdir $path: $!";
        my $proj = kvread('m0000.project');
        $params = $proj->{'params'};
        my $proj_title = $proj->{'prop.title'} // '(untitled)';
        print <<"EOS";
path = $path
params = $params
project = $proj_title
EOS
        if (%prop) {
            print "props {\n";
            foreach my $k (sort keys %prop) {
                my $v = $prop{$k};
                print "  $k = $v\n";
            }
            print "}\n";
        }
        foreach my $job (jobs()) {
            my ($b, $e) = @$job{qw(first last)};
            my $n = $b == $e ? $b : "$b-$e";
            my $t = $job->{'prop.title'} // '(untitled)';
            $params = $job->{'params'};
            print <<"EOS";
job $n {
    title = $t
    params = $params
}
EOS
        }
    }
    else {
        print <<"EOS";
path = $path
params = $params
EOS
    }
}
    
sub cmd_lp {
    my ($long);
    GetOptions(
        'l' => \$long,
    ) or usage;
    chdir "$root/projects" or fatal "Can't chdir $root/projects: $!";
    my @out;
    run3 [qw(find . -name m0000.project)], \undef, \@out, \undef;
    fatal "failed: ", $? >> 8 if $?;
    chomp @out;
    foreach (sort @out) {
        my ($path) = m{^\./(.+)/m0000.project$};
        if ($long) {
            my $proj = kvread($_);
            printf qq{%s "%s"\n}, $path, $proj->{'prop.title'} // '(untitled)';
            my @jobs = jobs($path);
            foreach my $job (@jobs) {
                my $n = $job->{'last'} - $job->{'first'} + 1;
                printf qq{  %4d .. %4d %s "%s"\n}, $job->{'first'}, $job->{'last'}, $job->{'date'}, $job->{'prop.title'} // '(untitled)';
            }
            print "\n";
        }
        else {
            print $path, "\n";
        }
    }
}
    
# --- Other functions

sub init {
    my ($path) = @_;
    -d $path or mkdir $path or fatal "Can't mkdir $path: $!";
    chdir $path or fatal "Can't chdir $path: $!";
    my %proj = ( 'params' => params2str(), props() );
    kvwrite('m0000.project', \%proj);
    return \%proj;
}

sub tty {
    if (!defined($tty)) {
        open $tty, '+<', '/dev/tty' or fatal "Can't open tty: $!";
    }
    print $tty @_ if @_;
    return $tty;
}

sub ask {
    my ($label, $rx, $default) = @_;
    fatal "Can't ask for $label" if !-t STDIN;
    while (1) {
        tty ucfirst($label), ': ';
        local $_ = <$tty>;
        if (defined $_) {
            chomp;
            return $default if !length && defined $default;
            return $_ if !defined $rx;
            return $_ if $_ =~ /$rx/;
        }
        else {
            return $default;
        }
    }
}

sub askbool {
    my $msg = @_ ? shift : '?';
    tty $msg, ' ';
    ReadMode 'cbreak';
    local $_ = ReadKey 0;
    ReadMode 'normal';
    tty "\n";
    return /^[Yy]/;
}

sub scan {
    my ($path) = @_;
    $prop{'title'} //= ask('title', qr/./);
    my @previously = all_images();
    my $i = next_image_num();
    ($width, $height) = paper($paper) if defined $paper;
    @now = localtime;
    my %job = (
        'user' => (getpwuid($<))[0],
        'date' => strftime('%Y%m%dT%H%M%S%z', @now),
        'params' => params2str(),
        props(),
    );
    kvwrite(mdfile($i, 'job'), \%job);
    my $source_arg = source2arg($source);
    my $mode_arg = mode2arg($mode);
    my @device = (defined $device ? (-d => $device) : ());
    my @flatbed = (
        $source eq 'flatbed'
            ? ('-e' => $i, '--source' => 'flatbed')
            : ()
    );
    while (1) {
        my @cmd = (
            $prog,
            @device,
            -o => IMTMPL,
            -x => $width,
            -y => $height,
            '--resolution' => $dpi,
            '--mode' => $mode_arg,
            '--source' => $source_arg,
            '--scan-script' => 'xz',
            -s => $i,
            @flatbed,
        );
        dryrun(@cmd), exit if $dryrun;
        askbool('Press any key when ready...') if $opt{'prompt'};
        run3 \@cmd, \undef, \undef, \undef;
        my ($err, $msg) = ($?, $!);
        fatal "scan failed: $msg" if $err;
        $i = next_image_num();
        last;
    }
    my %scanned = map { $_ => 1 } all_images();
    delete @scanned{@previously};
    my @newly = sort { $a <=> $b } keys %scanned;
    if (@newly) {
        my $first = $newly[0];
        my $last = $newly[-1];
        my %end = (
            'first' => $first,
            'last' => $last,
            'count' => scalar(@newly),
        );
        kvwrite(mdfile($newly[-1], 'end'), \%end);
        # system 'xz', map { sprintf IMTMPL, $_ } @newly;
        print join(' ', @newly), "\n";
    }
}

sub dryrun {
    print join(' ', map { quote($_) } @_), "\n";
}

sub mdfile($$) { sprintf MDTMPL, $_[0], $_[1] }

sub props { map { ("prop.$_" => $prop{$_}) } keys %prop }
sub params2str { join(' ', $mode, $dpi.'dpi', $paper || ($width.'x'.$height), $source) }

sub parse {
    my @list = @_;
    while (@list) {
        local $_ = shift @list;
        if (/^@/) {
            my $p = "$root/profiles/" . substr($_,1);
            my $profile = kvread($p);
            unshift @list, $profile->{'path'} if defined $profile->{'path'};
            unshift @list, split /\s+/, $profile->{'params'} if defined $profile->{'params'};
        }
        elsif (/^%/) {
            my $p = "$root/projects/" . substr($_,1);
            unshift @list, $p;
        }
        elsif (/^(bw|gray|color)$/i) {
            $mode = lc $_;
        }
        elsif (/^(flatbed|adf|duplex)$/) {
            $source = lc $1;
        }
        elsif (/^([0-9]+)dpi$/) {
            $dpi = $1;
        }
        elsif (/^(letter|legal|a4)$/) {
            $paper = $_;
            undef $width;
            undef $height;
        }
        elsif (/^${rxnum}x${rxnum}(in|cm|mm)?$/) {
            my $mul = defined $3 ? multiplier($3) : 1;
            ($width, $height) = ($1 * $mul, $2 * $mul);
            undef $paper;
        }
        elsif (/^([^=]+)=(.*)$/) {
            $prop{$1} = $2;
        }
        elsif ($_ =~ $rxpath) {
            push @path, $_;
        }
        elsif (/^([-+])(\S+)/) {
            $opt{$2} = ($1 eq '+');
        }
        else {
            fatal "Unrecognized: $_";
        }
    }
}

sub jobs {
    my ($dir) = @_;
    my $pfx = defined($dir) ? "$dir/" : '';
    my @j = map { num($_) } sort glob($pfx.'m*.job');
    my @e = map { num($_) } sort glob($pfx.'m*.end');
    my @jobs;
    foreach my $j (@j) {
        last if !defined(my $e = shift @e);
        my $job = kvread($pfx.mdfile($j, 'job'));
        $job->{'first'} = $j;
        $job->{'last'} = $e;
        push @jobs, $job;
    }
    return @jobs;
}

sub multiplier {
    my %mul = qw(
        in  25.4
        cm  10.0
        mm   1.0
    );
    return $mul{lc shift()} || usage;
}

sub kvread {
    my ($f) = @_;
    my %hash;
    open my $fh, '<', $f or fatal "Can't open $f: $!";
    while (<$fh>) {
        next if /^\s*(#.*)?$/;
        /^\s*([^\s=]+)\s*(?:=\s*)?(.*)$/
            or fatal "Bad line in profile $f $_";
        $hash{$1} = $2;
    }
    return \%hash;
}

sub kvwrite {
    my ($f, $hash) = @_;
    open my $fh, '>', $f or fatal "Can't open $f: $!";
    while (my ($k, $v) = each %$hash) {
        print $fh "$k = $v\n";
    }
}

sub path {
    my $path = $spool;
    foreach (map glob, @_) {
        if (/^\+/) {
            $path .= "/$_";
        }
        else {
            $path = $_;
        }
    }
    $path =~ s{/(?:\+/)+}{/}g;
    $path =~ s{(?<=/)\+$}{randhex()}e;
    $path =~ s/%([A-Za-z]|\([^()]+\))/expand($1)/eg;
    return $path;
}

sub expand {
    my ($str) = @_;
    if ($str =~ /^[YmdHMS]$/) {
        @now = localtime if !@now;
        return strftime('%'.$str, @now);
    }
    elsif ($str =~ /^\((.+)\)$/ && defined $prop{$1}) {
        return $prop{$1};
    }
    fatal "Can't expand: $str";
}

sub randhex {
    my ($fh, $buf);
    if (open $fh, '<', '/dev/random' and sysread $fh, $buf, 4) {
        return unpack('H7', $buf);
    }
    else {
        my $r = int(rand(0x0fffffff)) ^ int(rand(~0x0fffffff)) & 0x0fffffff;
        printf "%07x\n", $r;
    }
}

sub all_images {
    my @i = sort glob IMGLOB;
    s/$rximfile/$1/o for @i;
    return map { $_ + 0 } @i;
}

sub next_image_num {
    my @i = sort glob IMGLOB;
    return 1 if !@i;
    s/$rximfile/$1/o for @i;
    return $i[-1] + 1;
}

sub mode2arg {
    my %mode = qw( bw Lineart gray Gray grey Gray color Color colour Color );
    $mode{lc shift}
}

sub source2arg {
    my %source = ( 'adf' => 'ADF', 'duplex' => 'ADF Duplex', 'flatbed' => 'Flatbed' );
    $source{lc shift}
}

sub paper {
    my %dimens = (
        # ISO paper sizes
        'a5' => [ 148, 210 ],
        'a4' => [ 210, 297 ],
        'a3' => [ 297, 420 ],
        # U.S. paper sizes
        'letter'  => [  8.5*25.4, 11.0*25.4 ],
        'legal'   => [  8.5*25.4, 14.0*25.4 ],
        'tabloid' => [ 11.0*25.4, 17.0*25.4 ],
    );
    return @{ $dimens{lc shift} || usage };
}

sub num {
    return $1 + 0 if shift() =~ m{(?:.+/)?[a-z](\d+)};
    return 0;
}

sub run {
    my ($in, $out, $err) = @_;
    run3 [@_], \$in, \$out, \$err;
    system @_;
    return $? >> 8;
}

sub quote {
    local $_ = shift;
    return $_ if !/[\s\\"']/;
    return qq{'$_'} if !/[\\']/;
    s/(["\\])/\\$1/g;
    return qq{"$_"};
}

sub unique {
    my (%u, @u);
    foreach (@_) {
        push @u, $_ if !$u{$_}++;
    }
    return @u;
}

sub fatal {
    print STDERR 'insc: ', $_, "\n" for @_;
    exit 2;
}

sub quit {
    print STDERR $_, "\n" for @_;
    exit 0;
}

